plugins {
    id 'java'
    id 'maven'
    id 'org.hidetake.swagger.generator' version '1.4.0'
    id 'de.undercouch.download' version '3.1.2'
}

//Keep Android people happy by using 1.7 and not 1.8
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

import de.undercouch.gradle.tasks.download.Download
import org.hidetake.gradle.swagger.generator.GenerateSwaggerCode

dependencies {
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.2.1'
}

def swaggerGenerated = "$buildDir/swagger/"
def generated = "$projectDir/generated/"

sourceSets {
    main {
        java {
            srcDirs += "$generated/src/main"
        }
    }
    test {
        java {
            srcDirs += "$generated/src/test"
        }
    }
}

dependencies {
    compile "com.squareup.retrofit2:retrofit:2.0.2"
    compile "com.squareup.retrofit2:converter-scalars:2.0.2"
    compile "com.squareup.retrofit2:converter-gson:2.0.2"
    compile "io.swagger:swagger-annotations:1.5.8"
    compile "org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.1"
    compile "joda-time:joda-time:2.9.3"

    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-collections4:4.1'

    testCompile "junit:junit:4.12"
}

task swaggerDownload(type: Download) {
    src 'https://esi.tech.ccp.is/latest/swagger.json'
    dest file(new File(buildDir, 'swagger.json'))
    acceptAnyCertificate true
}

task swaggerBuild(type: GenerateSwaggerCode, dependsOn: 'swaggerDownload') {
    language = 'java'
    inputFile = file(new File(buildDir, 'swagger.json'))
    outputDir = file(swaggerGenerated)
    configFile = file('config.json')
}

task swaggerCopy(type: Copy) {
    from new File(swaggerGenerated, 'src')
    into new File(generated, 'src')
}

task swaggerDelete(type: Delete) {
    outputs.upToDateWhen { false }
    delete "$swaggerGenerated", "$generated/src/main/AndroidManifest.xml"
}

task swaggerGenerate(dependsOn: ['swaggerBuild', 'swaggerCopy', 'swaggerDelete']) {

}

clean.doFirst {
    delete "$generated"
}

compileJava.dependsOn swaggerGenerate